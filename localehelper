#!/usr/bin/perl

use strict;
use warnings;

use v5.10;

use File::Temp qw();

my $myself = 'localehelper';

my $category_re = qr/(?:LC_[A-Z]+|LANG|LANGUAGE)/;
my $locale_with_encoding_re = qr/^([^.@]+)[.]([^.@]+)((?:@.+)?)$/;

sub clean_environment($)
{
    my ($regex) = @_;
    my @keys_to_unset = grep { m/$regex/ } keys %ENV;
    delete @ENV{@keys_to_unset};
}

sub update_environment(%)
{
    my %env = @_;
    @ENV{keys(%env)} = values(%env);
}

sub normalize_encoding($)
{
    local ($_) = @_;
    y/A-Z/a-z/;
    s/[-_]//g;
    return $_;
}

sub normalize_locale($)
{
    local ($_) = @_;
    if (my ($prefix, $encoding, $suffix) = m/$locale_with_encoding_re/) {
        $encoding = normalize_encoding($encoding);
        return "$prefix.$encoding$suffix";
    }
    return $_;
}

my @extra_locales = ();
my %env = ();
while (scalar(@ARGV) > 0) {
    given ($ARGV[0]) {
        when (m/^($category_re)=(.*)$/) {
            my ($category, $locale) = ($1, $2);
            $env{$category} = $locale;
            shift @ARGV;
        }
        when ('-x') {
            shift @ARGV;
            if (not @ARGV) {
                print STDERR "$myself: error: -x requires an argument\n";
                exit 1;
            }
            push @extra_locales, split(',', shift @ARGV);
        }
        when ('--') {
            shift @ARGV;
            last;
        }
        default {
            last;
        }
    }
}

if (not @ARGV) {
    exit 0;
}

clean_environment(qr/^$category_re$/);

delete $ENV{LOCPATH};

my %all_locales = map { normalize_locale($_) => 1 } split(/\n/, `locale -a`);
my $no_missing_locales = 1;
my %locales_to_generate = ();
while (my ($category, $locales) = each %env) {
    next if $category eq 'LANGUAGE';
    local $" = ',';
    my @nlocales = map { normalize_locale($_) } split(',', $locales);
    $locales_to_generate{"@nlocales"} = 1;
    $no_missing_locales &&= grep { exists $all_locales{$_} } @nlocales;
}
for my $locale (@extra_locales) {
    $locale = normalize_locale($locale);
    $locales_to_generate{$locale} = 1;
    $no_missing_locales &&= exists $all_locales{$locale};
}

unless ($no_missing_locales) {
    %all_locales = ();
    my %encodings = ();
    for (glob '/usr/share/i18n/charmaps/*') {
        s{.*/}{};
        s/\.gz$//;
        my $encoding = $_;
        my $nencoding = normalize_encoding($_);
        $encodings{$nencoding} = $encoding;
    }
    my $locpath = $ENV{LOCPATH} = File::Temp::tempdir("$myself.XXXXXXXX", CLEANUP => 1, TMPDIR => 1);
    for my $locales (keys %locales_to_generate) {
        my @locales = split(',', $locales);
        for my $locale (@locales) {
            if (my ($prefix, $encoding, $suffix) = $locale =~ m/$locale_with_encoding_re/) {
                $encoding = $encodings{$encoding} // $encoding;
                $locale = "$prefix.$encoding$suffix";
                next if -d "/usr/lib/locale/$locale/";
                system('localedef', '-f', $encoding, '-i', "$prefix$suffix", "$locpath/$locale/");
                if ($?) {
                    print STDERR "$myself: warning: localedef for $locale failed\n";
                    next;
                }
                $all_locales{normalize_locale($locale)} = 1;
                last;
            } else {
                print STDERR "$myself: warning: cannot generate locale $locale\n";
            }
        }
    }
}

while (my ($category, $locales) = each %env) {
    next if $category eq 'LANGUAGE';
    my @all_locales = split(',', $locales);
    my @existing_locales = grep { exists $all_locales{normalize_locale($_)} } @all_locales;
    if (@existing_locales) {
        $env{$category} = $existing_locales[0];
    } else {
        local $" = ', ';
        print STDERR "$myself: error: missing locales @all_locales\n";
        exit 1;
    }
}

update_environment(%env);

system { $ARGV[0] } @ARGV;

exit $?;

# vim:ts=4 sw=4 et
