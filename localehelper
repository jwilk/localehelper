#!/usr/bin/perl

use strict;
use warnings;

use v5.10;

use File::Temp qw();

my $myself = 'localehelper';

my $category_re = qr/(?:LC_[A-Z]+|LANG|LANGUAGE)/;
my $locale_with_encoding_re = qr/^([^.@]+)[.]([^.@]+)((?:@.+)?)$/;

sub clean_environment($)
{
    my ($regex) = @_;
    my @keys_to_unset = grep { m/$regex/ } keys %ENV;
    delete @ENV{@keys_to_unset};
}

sub update_environment(%)
{
    my %env = @_;
    @ENV{keys(%env)} = values(%env);
}

sub normalize_encoding($)
{
    local ($_) = @_;
    y/A-Z/a-z/;
    s/[-_]//g;
    return $_;
}

sub normalize_locale($)
{
    local ($_) = @_;
    if (my ($prefix, $encoding, $suffix) = m/$locale_with_encoding_re/) {
        $encoding = normalize_encoding($encoding);
        return "$prefix.$encoding$suffix";
    }
    return $_;
}

my %env = ();
while (scalar(@ARGV) > 0) {
    given ($ARGV[0]) {
        when (m/^($category_re)=(.*)$/) {
            my ($category, $locale) = ($1, $2);
            $env{$category} = $locale;
            shift @ARGV;
        }
        when ('--') {
            shift @ARGV;
            last;
        }
        default {
            last;
        }
    }
}

if (not @ARGV) {
    exit 0;
}

clean_environment(qr/^$category_re$/);

delete $ENV{LOCPATH};

my %all_locales = map { normalize_locale($_) => 1 } split(/\n/, `locale -a`);
my $missing_locales = 0;
my %locales_to_generate = ();
while (my ($category, $locale) = each %env) {
    my $nlocale = normalize_locale($locale);
    next if $category eq 'LANGUAGE';
    $locales_to_generate{$nlocale} = 1;
    if (not exists $all_locales{$nlocale}) {
        $missing_locales = 1;
    }
}

if ($missing_locales) {
    my %encodings = ();
    for (glob '/usr/share/i18n/charmaps/*') {
        s{.*/}{};
        s/\.gz$//;
        my $encoding = $_;
        my $nencoding = normalize_encoding($_);
        $encodings{$nencoding} = $encoding;
    }
    my $locpath = $ENV{LOCPATH} = File::Temp::tempdir("$myself.XXXXXXXX", CLEANUP => 1, TMPDIR => 1);
    my @locales_to_generate = sort keys %locales_to_generate;
    for my $locale (keys %locales_to_generate) {
        if (my ($prefix, $encoding, $suffix) = $locale =~ m/$locale_with_encoding_re/) {
            $encoding = $encodings{$encoding};
            $locale = "$prefix.$encoding$suffix";
            next if -d "/usr/lib/locale/$locale/";
            system('localedef', '-f', $encoding, '-i', "$prefix$suffix", "$locpath/$locale/");
            if ($?) {
                print STDERR "$myself: error: localedef for $locale failed\n";
                exit 1;
            }
        } else {
            print STDERR "$myself: error: cannot generate locale $locale\n";
            exit 1;
        }
    }
}

update_environment(%env);

system { $ARGV[0] } @ARGV;

exit $?;

# vim:ts=4 sw=4 et
